
%import common.WS
_COMMENT : (/\/\/[^\n]*/)
WSORCOMMENT : WS | (_COMMENT "\n")
%ignore WSORCOMMENT

NAME : /[a-zA-Z_]+/
NUMBER : /[0-9]+/
COMPARISON : "=" | ">" | "<" | ">=" | "<=" | "!="
OPERATOR : "+" | "-" | "*" | "/"
DIRECTION: "UP" | "DOWN"

// note the explicit option for comments at the end, because lark doesn't support $ in the definition of _COMMENT
program : codeblock? (labelledcodeblock)* label? _COMMENT*

codeblock : _statement+

labelledcodeblock: label _statement*

label : ":" NAME

_statement : _ifstatement | goto | vardeclaration | varassignment | push | move | halt | pass

_ifstatement : ifcomparison | ifread
ifcomparison : "IF" expression COMPARISON expression "{" codeblock "}"
ifread : "IF" "READ" NAME "{" codeblock "}"

goto: "GOTO" NAME

vardeclaration : "VAR" NAME "=" number "/" number

varassignment : NAME "=" expression

expression : NAME | number | expression OPERATOR expression | "(" expression ")"

push : "PUSH" NAME

move : "MOVE" DIRECTION

halt: "HALT"

pass: "PASS"

number: NUMBER
